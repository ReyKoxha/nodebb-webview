// Main build script for app
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build production APK
//        gradle wrapper                  Create Gradle Wrapper


buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:[0.10.1, 0.99.99]'
        classpath 'com.google.gms:google-services:3.2.0'
    }
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

repositories {
    maven { url 'https://maven.google.com' }
}

final VERSION_MAJOR = 3 // Max 2 digits
final VERSION_MINOR = 0 // Max 2 digits
final VERSION_PATCH = 0 // Max 2 digits
final VERSION_BUILD = 0 // Max 3 digits

apply plugin: 'com.android.application'

dependencies
{
	implementation 'com.android.support:support-v4:27.1.1'
	implementation 'com.android.support:appcompat-v7:27.1.1'
	implementation "com.google.android.gms:play-services-base:15.0.1"
	implementation "com.google.android.gms:play-services-analytics:16.0.0"
	implementation "com.google.android.gms:play-services-ads:15.0.1"
	implementation "com.google.android.gms:play-services-gcm:15.0.1"
	implementation "com.google.android.gms:play-services-location:15.0.1"
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
	implementation 'com.onesignal:OneSignal:[3.9.1, 3.99.99]'
	implementation 'commons-io:commons-io:2.6'
}


android
{
	compileSdkVersion 27
	buildToolsVersion '27.0.3'

	defaultConfig
	{
		multiDexEnabled true
		minSdkVersion 19
		targetSdkVersion 27
		versionCode VERSION_MAJOR*10000000 + VERSION_MINOR*100000 + VERSION_PATCH*1000 + VERSION_BUILD
		versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"

		manifestPlaceholders = [manifestApplicationId: "${applicationId}",
								onesignal_app_id: "YOURAPPID",
								// Project number pulled from dashboard, local value is ignored.
								onesignal_google_project_number: "REMOTE"]
	}

	lintOptions {
		checkReleaseBuilds false
	}

	signingConfigs
	{
		release
		{
			// Obtain Password and Alias
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes
	{
		debug
		{
			versionNameSuffix "-debug"
		}

		release
		{
			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.all { output ->
			def newApkName = applicationId + "-" + variant.versionName + "(" + variant.versionCode + ")" + ".apk";
			outputFileName = new File(variant.name, newApkName);
		}
	}
}


task askForPasswords <<
{
	def storePass
	def keyAlias
	def keyPass

	def keystorePropertiesFile = new File(project.property("keystore.properties"))

	if(project.hasProperty("keystore.properties") && keystorePropertiesFile.exists())
	{
		println "Loading keystore passwords from property file..."
		Properties properties = new Properties()
		properties.load(new FileInputStream(keystorePropertiesFile))
		storePass = properties['keystore.store.password']
		keyAlias  = properties['keystore.key.alias']
		keyPass  = properties['keystore.key.password']
	}
	else
	{
		println "Getting keystore passwords..."
		storePass = new String(System.console().readPassword("\nStore password: "))
		keyAlias  = new String(System.console().readLine("Key alias: "))
		keyPass  = new String(System.console().readPassword("Key password: "))
	}

	android.signingConfigs.release.storePassword = storePass
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPass
}


tasks.whenTaskAdded
{
	theTask ->
	if(theTask.name.equals("packageRelease"))
	{
		theTask.dependsOn "askForPasswords"
	}
}
